# Processing of Markdown documents.
import builder.html
import markdown.html (Filter)
import std.set (Set)
import syntax (Languages)
import syntax.format (Html)

# A filter that applies syntax highlighting to a Markdown document.
#
# This filter processes fenced code blocks that are tagged with a language, such
# as the following:
#
# ````markdown
# ```inko
# this_is_inko_code
# ```
# ````
#
# Syntax highlighting is performed using
# [inko-syntax](https://github.com/yorickpeterse/inko-syntax).
type pub SyntaxHighlight {
  let @languages: Languages

  fn pub static new -> SyntaxHighlight {
    SyntaxHighlight(Languages.new)
  }

  fn mut highlight(pre: ref html.Element) -> Option[html.Element] {
    let code = match pre.nodes.get(0) {
      case Ok(Element(el)) if el.name == 'code' and el.nodes.size == 1 -> el
      case _ -> return Option.None
    }

    let lang = try code.attributes.get('data-language').ok.then(fn (v) {
      @languages.get(v)
    })

    let bytes = match code.nodes.get(0) {
      case Ok(Text(v)) -> v.to_byte_array
      case _ -> return Option.None
    }

    match Html.new.format(lang.lexer(bytes)) {
      case { @nodes = nodes } -> {
        match nodes.pop {
          case Some(Element(el)) -> Option.Some(el)
          case _ -> Option.None
        }
      }
    }
  }

  fn mut replace(element: mut html.Element) {
    # We modify the `pre` element in-place, as swapping it with the new element
    # results in dropping of the old element while references (as part of the
    # iteration) to it still exist.
    match highlight(element) {
      case Some({ @name = name, @attributes = attr, @nodes = nodes }) -> {
        element.name = name
        element.attributes = attr
        element.nodes = nodes
        {}
      }
      case _ -> {}
    }
  }
}

impl Filter for SyntaxHighlight {
  fn pub mut run(document: mut html.Document) {
    let nodes = document.nodes.iter_mut.to_array

    loop {
      match nodes.pop {
        case Some(Element(el)) if el.name == 'pre' and el.nodes.size == 1 -> {
          replace(el)
        }
        case Some(Element(el)) -> nodes.append(el.nodes.iter_mut.to_array)
        case Some(_) -> {}
        case _ -> break
      }
    }
  }
}

# A filter that adds support for admonitions (notes, warnings, etc).
#
# Admonitions use the custom block syntax of inko-markdown. For example, to
# create a note you'de use the following syntax:
#
# ```markdown
# ::: note
# The body goes here.
# :::
# ```
#
# The following admonition types are supported:
#
# - discuss
# - info
# - note
# - tip
# - warn
type pub Admonitions {
  # The admonition classes that are supported.
  let pub @types: Set[String]

  # The base class to apply to the container.
  let pub mut @class: String

  # The element name to use for the icon.
  let pub mut @icon_element: String

  # The class to apply to the icon element.
  let pub mut @icon_class: String

  # The name of the class to apply to the text div.
  let pub mut @text_class: String

  fn pub static new -> Admonitions {
    let types = Set.new

    types.insert('info')
    types.insert('tip')
    types.insert('note')
    types.insert('warn')
    types.insert('discuss')

    Admonitions(
      types: types,
      class: 'admonition',
      icon_element: 'i',
      icon_class: 'icon',
      text_class: 'text',
    )
  }

  fn replace(node: mut html.Node) {
    let note = match node {
      case Element(el) if el.name == 'div' -> el
      case _ -> return
    }

    let kind = match note.attributes.get('class') {
      case Ok(v) if @types.contains?(v) -> v
      case _ -> return
    }

    let nodes = note.take_nodes
    let icon = note.element(@icon_element)
    let text = note.element('div')

    note.attr('class', '${@class} ${kind}')
    icon.attr('class', @icon_class)
    text.attr('class', @text_class)
    text.nodes = nodes
  }
}

impl Filter for Admonitions {
  fn pub mut run(document: mut html.Document) {
    for node in document.nodes.iter_mut { replace(node) }
  }
}
